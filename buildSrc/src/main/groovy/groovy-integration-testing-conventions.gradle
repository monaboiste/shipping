plugins {
  id("idea")
  id("groovy")
}

sourceSets {
  integrationTest {
    groovy.srcDir("src/integration-test/groovy")
    resources.srcDir("src/integration-test/resources")
  }
}

configurations {
  // integrationTest inherits dependencies from test source set
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

idea {
  module {
    testSources.from(sourceSets.integrationTest.groovy.srcDirs)
  }
}

dependencies {
  integrationTestImplementation(libs.jackson)
}

tasks.register("integration-test", Test) {
  group("verification")
  description("Run Integration tests")

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  useJUnitPlatform()
  testLogging {
    showStandardStreams = true
  }
}